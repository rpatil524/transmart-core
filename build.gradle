buildscript {
    ext {
        assetPipelineVersion = '2.11.6'
        grailsVersion = '3.2.13'
        guavaVersion = '19.0'
        jansiVersion = '1.14'
        protobufVersion = '3.1.0'
    }
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
        maven { url 'https://repo.grails.org/grails/core' }
        maven { url 'https://dl.bintray.com/grails/plugins' }
        // maven { url 'https://repo.transmartfoundation.org/content/repositories/public' }
        maven { url 'https://repo.thehyve.nl/content/repositories/public' }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "com.google.guava:guava:$guavaVersion"
        classpath "com.bertramlabs.plugins:asset-pipeline-gradle:$assetPipelineVersion"
        classpath 'org.grails.plugins:hibernate5:6.0.4'
        classpath "com.google.protobuf:protobuf-java:${protobufVersion}"
        classpath "com.google.protobuf:protobuf-java-util:${protobufVersion}"
        classpath 'org.grails.plugins:views-gradle:1.0.12'
        classpath 'net.linguica.gradle:maven-settings-plugin:0.5'
        classpath "org.fusesource.jansi:jansi:${jansiVersion}"
        classpath 'org.owasp:dependency-check-gradle:3.2.1'
    }
}

// Workaround suggested in https://github.com/gradle/gradle/issues/778
org.fusesource.jansi.AnsiConsole.out.println('[init jansi library conflict workaround]')

apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'org.owasp.dependencycheck'

ext {
    transmartVersion = '17.2.6'

    assetPipelineVersion = '2.11.6'
    gradleWrapperVersion = '3.5.1'
    grailsVersion = '3.2.13'
    guavaVersion = '19.0'
    jacksonVersion = '2.10.1'
    liquibaseVersion = '3.7.0'
    ojdb7Version = '12.1.0.1'
    openCsvVersion = '5.0'
    postgresqlVersion = '42.2.5.jre7'
    protobufVersion = '3.1.0'
    quartzVersion = '2.0.13'
    springSecurityPluginVersion = '3.2.3'
    springSecurityVersion = '4.1.4.RELEASE'
    keycloakVersion = '6.0.1'
}

task wrapper(type: Wrapper) {
    gradleVersion = gradleWrapperVersion
}

version = transmartVersion
group = 'org.transmartproject'

allprojects {
    ext {
        assetPipelineVersion = assetPipelineVersion
        guavaVersion = guavaVersion
        jacksonVersion = jacksonVersion
        liquibaseVersion = liquibaseVersion
        ojdb7Version = ojdb7Version
        openCsvVersion = openCsvVersion
        postgresqlVersion = postgresqlVersion
        protobufVersion = protobufVersion
        quartzVersion = quartzVersion
        springSecurityPluginVersion = springSecurityPluginVersion
        springSecurityVersion = springSecurityVersion
    }
    repositories {
        repositories {
            mavenLocal()
            mavenCentral()
            maven { url 'https://repo.grails.org/grails/core' }
            // maven { url 'https://repo.transmartfoundation.org/content/repositories/public/' }
            maven { url 'https://repo.thehyve.nl/content/groups/public/' }
            maven { url 'https://dl.bintray.com/bluesliverx/grails-plugins' }
            // maven { url 'https://jcenter.bintray.com' }
        }
    }
}

subprojects { project ->
    version = transmartVersion
    group = 'org.transmartproject'

    apply plugin: 'eclipse'
    apply plugin: 'idea'

    if (project.name =~ /^(transmart-core-db-tests|transmart-core-db|transmart-rest-api|transmart-api-server)$/) {
        if (project.name =~ /^(transmart-api-server)$/) {
            apply plugin: 'war'
            apply plugin: 'org.grails.grails-web'
        } else {
            apply plugin: 'org.grails.grails-plugin'
            bootRepackage.enabled = false
        }
        apply plugin: 'org.grails.grails-gsp'
        apply plugin: 'asset-pipeline'

        // See https://docs.gradle.org/3.2/release-notes.html#usage-of-jansi-library-embedded-with-java-annotation-processor
        tasks.withType(JavaCompile) {
            options.fork = true
        }

        dependencyManagement {
            imports {
                mavenBom "org.grails:grails-bom:$grailsVersion"
            }
            applyMavenExclusions false
        }
    }
    if (project.name =~ /^(transmart-core-api|transmart-data|transmart-copy|transmart-api-server|transmart-schemas)$/) {
        apply plugin: 'net.linguica.maven-settings'
        apply plugin: 'maven-publish'
        publishing {
            repositories {
                maven {
                    name "nl.thehyve.nexus"
                    url "https://repo.thehyve.nl/content/repositories/releases/"
                }
            }
        }
    }
}

task prepareTestDatabase(type: Exec) {
    commandLine './scripts/prepare_database.sh'
}
prepareTestDatabase.dependsOn 'transmart-schemas:assemble'
